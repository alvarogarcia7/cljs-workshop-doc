ClojureScript Tutorial
======================
Andrey Antukh, <niwi@niwi.be>
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:

Introduction
------------

This tutorial consists on to provide an introduction to clojurescript from very basic setup, to more
complex application in little incremental steps.

It includes:

- Setup initial clojure app layout.
- Setup initial clojurescript app layout.
- First contact to clojurescript language.
- Working with dom events.
- Working with routing in the browser.
- Working with ajax requests.
- First contact with core.async.
- Working with events and ajax using core.async.
- First contact with om/reactjs.
- Working with om and timetraveling.
- Working with om and peristent state.
- Little bonus: browser enabled repl.

Transversal:

- Little touch on Google Closure Library along all tutorial.
- ClojureScript features (not explained on the first contact with language).
- Some differences between Clojure and ClojureScript.


Why ClojureScript
-----------------

- ClojureScript is designed as guest language (unlike funscript or similar, it not intends to translate
  host code to js, you can not import java.util.Date on clojurescript...)
- Language with own semantics (not like coffeescript, typescript, ...)
- Good host interoperability.
- Batteries included (ClojureScript runtime & google closure library)
- Expressivenes
- Functional.
- Lisp.
- Macros.
- Google Closure Compiler (advanced code compiling with dead code elimination)
- core.async (coroutines and csp as a library)
- ... much more.


Setup initial project
---------------------

Let start with
~~~~~~~~~~~~~~

[source, text]
----
git clone https://github.com/niwibe/cljs-workshop
git checkout step0
----

This step consists on...
~~~~~~~~~~~~~~~~~~~~~~~~

- Initial leiningen project template.
- Add ring, compojure and other related dependencies.
- Create routes and initial application entry point.
- First run of hello world app.


Initial project tree
~~~~~~~~~~~~~~~~~~~~~

[source, text]
----
resources/
resources/public/
resources/index.html
src/
src/clj/
src/clj/cljsworkshop/
src/clj/cljsworkshop/core.clj
project.clj
----

Initial *project.clj*
~~~~~~~~~~~~~~~~~~~~~

[source, clojure]
----
(defproject cljsworkshop "0.1.0-SNAPSHOT"
  :description "FIXME: write description"
  :url "http://example.com/FIXME"
  :license {:name "BSD (2-Clause)"
            :url "http://opensource.org/licenses/BSD-2-Clause"}
  :dependencies [[org.clojure/clojure "1.6.0"]

                 ;; Backend dependencies
                 [compojure "1.3.1"]

                 [ring/ring-core "1.3.2" :exclusions [javax.servlet/servlet-api]]
                 [ring/ring-servlet "1.3.2" :exclusions [javax.servlet/servlet-api]]
                 [ring/ring-defaults "0.1.2" :exclusions [javax.servlet/servlet-api]]

                 [cc.qbits/jet "0.5.4"]]

  :source-paths ["src/clj"]
  :main cljsworkshop.core)
----


Intial ring/compojure handlers
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

- Ring handler consists in a simple function that receives a req (hash-map) and return a response (also hash-map).
- Compojure add routing handlers and some response helpers.
- jetty9 is a embedded http/application server.

._clj/cljsworkshop/core.clj_
[source, clojure]
----
(ns cljsworkshop.core
  (:require [qbits.jet.server :refer [run-jetty]]
            [ring.middleware.defaults :refer [wrap-defaults site-defaults]]
            [compojure.core :refer :all]
            [compojure.route :as route]
            [compojure.response :refer [render]]
            [clojure.java.io :as io]))

;; This is a handler that returns the
;; contents of `resources/index.html`
(defn home
  [req]
  (render (io/resource "index.html") req))

;; Defines a handler that acts as router
(defroutes app
  (GET "/" [] home)
  (route/resources "/static")
  (route/not-found "<h1>Page not found</h1>"))

;; Application entry point
(defn -main
  [& args]
  (let [app (wrap-defaults app site-defaults)]
    (run-jetty {:ring-handler app :port 5050})))
----


Initial _resources/index.html_ file
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

[source, html]
----
<!doctype html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>CLJS Workshop</title>
  </head>
  <body>
    <h1>Hello World</h1>
  </body>
</html>
----


Run your first clojure web application
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

As we declared the main entry point on our `project.clj`, now we only should
execute a _run_ command of leiningen:

[source, bash]
----
$ lein run
2014-12-08 14:03:49.623:INFO::main: Logging initialized @877ms
2014-12-08 14:03:52.992:INFO:oejs.Server:main: jetty-9.2.3.v20140905
2014-12-08 14:03:53.016:INFO:oejs.ServerConnector:main: Started ServerConnector@3149409c{HTTP/1.1}{0.0.0.0:5050}
2014-12-08 14:03:53.017:INFO:oejs.Server:main: Started @4283ms
----


First steps with ClojureScript
------------------------------

Is a little introduction to ClojureScript language. Is a proper step before starting working with it.

Data Types
~~~~~~~~~~

.Types
|===
| Type name | Representation
| String    | `"Hello World"`
| Long      | `2`
| Float     | `2.4`
| Keyword   | `:foo`
| Map       | `{:foo "bar"}`
| Vector    | `[1 2 "3"]`
| List      | `(1 2 "3")`
| Set       | `#{1 2 3}`
| Regex     | `#"^\w+$"`
|===


Print to console
~~~~~~~~~~~~~~~~

[source, clojure]
----
(.log js/console "hello world")
----

[source, clojure]
----
(enable-console-print!)
(println "hello world")
----


Modules
~~~~~~~

.Declare module
[source, clojure]
----
(ns my.library)
----

.Require a module
[source, clojure]
----
(ns my.library
  (:require [my.other :as other]))
----


Variables
~~~~~~~~~

.Top level
[source, clojure]
----
(def myvar "foo")
----

.Local
[source, clojure]
----
(let [myvar "foo"]
  (println myvar))
----


Functions
~~~~~~~~~

.Simple function definition
[source, clojure]
----
(defn foo
  [a b c]
  c)

(foo 1) ;; WARNING: function called with incorrect
        ;; number of arguments

(foo 1 2 3) ;; => 3
----

.Dispatch on arity
[source, clojure]
----
(defn foo
  ([a] "one")
  ([a b] "two")
  ([a b c] "three"))

(foo 1) ;; => "one"
(foo 1 2) ;; => "two"
(foo 1 2 3) ;; => "three"

;; Under advanced compilation direct dispatch to
;; arity. No arguments object manipulation
----

.Variable number of arguments
[source, clojure]
----
(defn foo
  [a b & rest]
  rest)

(foo 1 2 3) ;; => [3]
(foo 1 2 3 4 5) ;; => [3 4 5]
----

.Named parameters & default values
[source, clojure]
----
(defn foo
  [& {:keys [bar baz]
      :or {bar "default1"
           baz "default2"}}]
  (str bar "-" baz))

(foo) ;; => "default1-default2"
(foo :bar 1) ;; => "1-default2"
(foo :bar 1 :baz 2) ;; => "1-2"
----


Equality
~~~~~~~~

Is always based on value. CLJS does not have coercive equality.

[source, javascript]
----
// == operator is coercive
1 == "1" // => true

// sometimes based on value
{} == {} // => false

["a"] === ["a"] // => false
----

[source, clojure]
----
(= 1 "1") ;; => false
(= {} {}) ;; => true
(= ["a"] ["a"]) ;; => true
----


Immutable locals
~~~~~~~~~~~~~~~~

In cljs locals are immutable:

.This code throws an error:
[source, clojure]
----
(let [x 2]
  (set! x 3))
----


Dynamic binding
~~~~~~~~~~~~~~~

[source, clojure]
----
(def ^:dynamic x 5)

(defn print-value
  []
  (println "Current value:" x))

(print-value)
(binding [x 10]
  (print-value))
(print-value)

;; Will result in:
;; Current value: 5
;; Current value: 10
;; Current value: 5
----


Destructuring
~~~~~~~~~~~~~

.Positional destructuring.
[source, clojure]
----
(def color [255 255 100 0.5])

(let [[r g _ a] color]
  (println r)
  (println a))

;; Will result in:
;; 255
;; 0.5
----

.Hash map keys destructuring
[source, clojure]
----
(def m {:first "Bob"
        :middle "J"
        :last "Smith"})

(let [{:keys [first last]} m]
  (println first)
  (println last))

;; Will result in:
;; Bob
;; Smith
----


Expression problem
~~~~~~~~~~~~~~~~~~

[source, clojure]
----
;; For example say you'd like to use RegExps
;; as functions

(extend-type js/RegExp
  IFn
  (-invoke
   ([this s]
     (re-matches this s))))

(filter #"foo.*" ["foo" "bar" "foobar"])
;; => ("foo" "foobar")
----


More resources:

- http://www.ibm.com/developerworks/library/j-clojure-protocols/


Multimethods
~~~~~~~~~~~~

Polymorphism a la carte.

.Define a multimethod
[source, clojure]
----
(defmulti say-hello
  (fn [person]
    (:lang person :en)))

(defmethod say-hello :en
  [person]
  (format "Hello %s" (:name person)))

(defmethod say-hello :es
  [person]
  (format "Hola %s" (:name person)))
----

.Playing with multimethod
[source, clojure]
----
(def person-alex {:lang :es :name "Alex"})
(def person-yen {:lang :en :name "Yen"})
(def person-anon {:name "Anonymous"})

(say-hello person-alex)
;; => "Hola Alex"

(say-hello person-yen)
;; => "Hello Yen"

(say-hello person-anon)
;; => "Hello Anonimous"
----


Interoprability
~~~~~~~~~~~~~~~


Create javascript objects
^^^^^^^^^^^^^^^^^^^^^^^^^

.ClojureScript
[source, clojure]
----
(def foo (js-obj "bar" "baz"))
----

.Javascript
[source, javascript]
----
var foo = {bar: "baz"};
----


Property access
^^^^^^^^^^^^^^^

.ClojureScript
[source, clojure]
----
(set! (.-bar foo) "baz")
(.log js/console (.-bar foo))

;; aset means array set
(aset foo "abc" 17)
(.log js/console (aget foo "abc"))
----

.Javascript
[source, javascript]
----
foo.bar = "baz";
console.log(foo.bar);

foo["abc"] = 17;
console.log(foo["abc"]);
----


Conversions beween cljs and js
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

.Convert cljs types to js using `clj->js` function
[source, clojure]
----
(let [a {:a 1 :b {:c 1}}]
  (clj->js a))
----

.Convert js types to cljs using `js->clj` function
[source, clojure]
----
(defn get-names [people]
  (let [people (js->clj people)
        names (map "name" people)]
    (clj->js names)))
----

.Using reader macro for conver cljs to js:
[source, clojure]
----
(let [a #js [1 2 3]]
  (println (aget a 1)))

;; Will result in:
;; 2
----

NOTE: the #js reader macro is not recursive.


More
~~~~

http://himera.herokuapp.com/synonym.html


First ClojureScript Hello World
-------------------------------

Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step1
----


New dependencies on *project.clj*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

._project.clj_
[source, clojure]
----
:dependencies [;; ...
               [org.clojure/clojurescript "0.0-2843"]
               ;; ...]
----


*lein-cljsbuild* plugin is your friend
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

._project.clj_
[source, clojure]
----
:plugins [[lein-cljsbuild "1.0.4"]]
:cljsbuild {:builds
            [{:id "app"
              :source-paths ["src/cljs"]
              :compiler {:output-to "resources/public/js/app.js"
                         :output-dir "resources/public/js/out"
                         :source-map true
                         :optimizations :none
                         :asset-path "/static/js/out"
                         :main "cljsworkshop.core"
                         :pretty-print true}}]}
----


New ClojureScript file.
~~~~~~~~~~~~~~~~~~~~~~~

New tree structure on `src/` directory for clojurescript sources.

[source, text]
----
src/cljs/
src/cljs/cljsworkshop/
src/cljs/cljsworkshop/core.cljs
----


Hello World in cljs
~~~~~~~~~~~~~~~~~~~

A simple main function that sets a helloworld as html in one dom element:

._core.cljs_
[source, clojure]
----
(defn set-html! [el content]
  (set! (.-innerHTML el) content))

(defn main
  []
  (let [content "Hello World from Clojure Script"
        element (aget (js/document.getElementsByTagName "main") 0)]
    (set-html! element content)))
----


*index.html* changes
~~~~~~~~~~~~~~~~~~~~

Adapt our template for make it compatible for our example:

[source, html]
----
<body>
  <main></main>
  <script src="/static/js/app.js"></script>
</body>
----


Compile our cljs file
~~~~~~~~~~~~~~~~~~~~~

[source, text]
----
[3/5.0.7]niwi@niwi:~/cljs-workshop> lein cljsbuild once
Compiling ClojureScript.
Compiling "resources/public/js/app.js" from ["src/cljs"]...
Successfully compiled "resources/public/js/app.js" in 3.396 seconds.
----


First DOM events
----------------

Introduction
~~~~~~~~~~~~

This section shows a basic way to access to dom and dom events using
google closure library.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step2
----


*index.html* changes
~~~~~~~~~~~~~~~~~~~~

Adapt our initial template to something that we can use for dom events
examples. It consists in two pieces:

- One button for increment the counter.
- One span for show the current value of counter.


._/resources/index.html_
[source, html]
----
<main>
  <section>
    <span>Clicks: </span>
    <span id="clicksnumber"><span>
  </section>
  <button id="button">Click me</button>
</main>
----

New *main* function
~~~~~~~~~~~~~~~~~~~

Change the main function to something like this:

[source, clojure]
----
(ns cljsworkshop.core
  (:require [goog.events :as events]
            [goog.dom :as dom]))

(defn main
  []
  (let [counter (atom 0)
        button  (dom/getElement "button")
        display (dom/getElement "clicksnumber")]

    ;; Set initial value
    (set! (.-innerHTML display) @counter)

    ;; Assign event listener
    (events/listen button "click"
                   (fn [event]
                     ;; Increment the value
                     (swap! counter inc)
                     ;; Set new value in display element
                     (set! (.-innerHTML display) @counter)))))
----


New stuff
~~~~~~~~~

- ClojureScript uses Google Closure Library for modules/namespace: *each ClojureScript file reprensents a google closure module*
- The `:require` statement on `ns` can loads any google closure module or your defined module that the compiler can find in the path (see _project.clj_ for path...)
- Google Closure Library comes with ClojureScript. You don't need add it as dependency.
- Works in advanced mode of google closure compiler (that eliminates unused code).


Routing in the browser
----------------------

Introduction
~~~~~~~~~~~~

Little preview of routing in the browser. Some javascript frameworks comes with own solution, but in
this case we simply use an library dedicated to routing: secretary.

Clojurescript community have other libraries but this one is the most easy understand for newcomers.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step3
----

Add *secretary* dependency
~~~~~~~~~~~~~~~~~~~~~~~~~~

Add the corresponding dependency entry:

._project.clj_
[source, clojure]
----
:dependencies [;; ...
               [secretary "1.2.1"]]
----


*core.cljs* changes
~~~~~~~~~~~~~~~~~~~

Adapt our cljs code to something like this:

[source, clojure]
----
(ns cljsworkshop.core
  (:require-macros [secretary.core :refer [defroute]])
  (:require [goog.events :as events]
            [goog.dom :as dom]
            [secretary.core :as secretary])
  (:import goog.History))

(def app (dom/getElement "app"))

(defn set-html! [el content]
  (set! (.-innerHTML el) content))

(defroute home-path "/" []
  (set-html! app "<h1>Hello World from home page.</h1>"))

(defroute some-path "/:param" [param]
  (let [message (str "<h1>Parameter in url: <small>" param "</small>!</h1>")]
    (set-html! app message)))

(defroute "*" []
  (set-html! app "<h1>Not Found</h1>"))

(defn main
  []
  ;; Set secretary config for use the hashbang prefix
  (secretary/set-config! :prefix "#")

  ;; Attach event listener to history instance.
  (let [history (History.)]
    (events/listen history "navigate"
                   (fn [event]
                     (secretary/dispatch! (.-token event))))
    (.setEnabled history true)))

(main)
----

New stuff
~~~~~~~~~

- ClojureScript macros should be written in Clojure (not ClojureScript) but should emit ClojureScript code.
- Should be imported separatedly, using `(:require-macros ...)` statement on `ns`.
- Google closure classes should be imported with `(:import ...)` statement.


First steps with Ajax
---------------------

Introduction
~~~~~~~~~~~~

This step introduces some experiments with ajax. It uses a google closure library facilities
for make easy JSONP requests to the wikipedia.

This is a simple demostration on how can it be done, using plain callback style code. But in future
step you will discover better ways to do it.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step4
----


*core.cljs* changes
~~~~~~~~~~~~~~~~~~~

.Partial content from _core.cljs_
[source, clojure]
----
(ns cljsworkshop.core
  (:require-macros [secretary.core :refer [defroute]])
  (:require [goog.events :as events]
            [goog.dom :as dom]
            [secretary.core :as secretary])
  (:import goog.History
           goog.Uri
           goog.net.Jsonp))

(def search-url "http://en.wikipedia.org/w/api.php?action=opensearch&format=json&search=")
(def home-html
  (str "<h1>Wikipedia Search:</h1>"
       "<section>"
       "  <input id=\"query\" placeholder=\"Type your search...\" />"
       "  <button id=\"searchbutton\">Search</button>"
       "  <ul id=\"results\"></ul>"
       "</section>"))

(defn render-results [results]
  (let [results (js->clj results)]
    (reduce (fn [acc result]
              (str acc "<li>" result "</li>"))
            ""
            (second results))))

(defn do-jsonp
  [uri callback]
  (let [req (Jsonp. (Uri. uri))]
    (.send req nil callback)))

(defroute home-path "/" []
  (set-html! app home-html)
  (let [on-response     (fn [results]
                          (let [html (render-results results)]
                            (set-html! (dom/getElement "results") html)))

        on-search-click (fn [e]
                          (let [userquery (.-value (dom/getElement "query"))
                                searchuri (str search-url userquery)]
                            (do-jsonp searchuri on-response)))]

    (events/listen (dom/getElement "searchbutton") "click" on-search-click)))
----


Introduction to core.async
--------------------------

Is a CSP library with steroids.


Async code looks sync
~~~~~~~~~~~~~~~~~~~~~

Before start with core.async, we will try to solve one simple problem:

1. Request 1 url page.
2. Wait 1second
3. Request 2 url page.
4. Return result both results.


Solution using ES5
^^^^^^^^^^^^^^^^^^

Let start introducing a problem using ES5 (EcmaScript 5) or shortly javascript of
today. For it, firstly define the following utils functions:

[source, javascript]
----
function timeout(ms) {
  return new Promise(function(resolve) {
    setTimeout(resolve, ms);
  });
}

function httpGet(url) {
  return new Promise(function(resolve) {
    var req = new XMLHttpRequest();
    req.open("GET", url, false);
    req.send(null);
    req.onreadystatechange = function() {
      if (req.readyState == 4) {
        resolve(xhr.responseText);
      }
    }
  });
}
----

And implement the solution:

[source, javascript]
----
function doStuff() {
   return httpGet("http://page1/").then(function(response) {
    return timeout(1000).then(function() {
      return response;
    });
   })
   .then(function(response1) {
     return httpGet("http://page2/").then(function(response2) {
       return {response1: response1,
               response2: response2};
     });
   })
}
----

.Now you can use it so:
[source, javascript]
----
doStuff().then(function(result) {
  console.log(result.response1);
  console.log(result.response2);
});
----

Obviously, it can be done better, but nobody will save us from callbacs.


Solution using ES7
^^^^^^^^^^^^^^^^^^

But, what is cooking for ES7? (ES7? but ES6 still not ready? WTF)

.Same example but using the draft proposal for ES7
[source, javascript]
----
async function doStuff() {
  var response1, response2;

  response1 = await httpGet("http://page1/");
  await timeout(1000):
  response2 = await httpGet("http://page2/");
  return {response1: response1,
          response2: response2};
}
----

.Now you can use it so:
[source, javascript]
----
(async function() {
  var result = await doStuff()
  console.log(result.response1);
  console.log(result.response2);
})();
----

Now looks much better.

Notes:

- This can be "emulated" with generators, but them are not designed for this purpose.


Solution using cljs and core.async
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Now having the background of ES7 example, let see same thing but using
core.async library with clojure script.

.Define the missing util function.
[source, clojure]
----
(defn http-get [uri]
  (let [out (chan)
        req (XhrIo. (Uri. uri))]
    (events/listen req "success" #(put! out (.getResponseText (.-target %))))
    (.send req (Uri. uri))
    out))
----

.Define the doStuff like function with main logic.
[source, clojure]
----
(defn do-stuff
  []
  (go
    (let [response1 (<! (http-get "http://page1/"))
          _         (<! (timeout 1000))
          response2 (<! (http-get "http://page2/"))]
      {:response1 response1
       :response2 response2})))
----

.Now see an example of how use it.
[source, clojure]
----
(go
  (let [result (<! (do-stuff))]
    (.log js/console (.-response1 result))
    (.log js/console (.-response2 result))))
----


You can see that the code that is async by nature, it seems to be like synchronous.


A little reference of basic core.async components
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

The *go* function/macro
^^^^^^^^^^^^^^^^^^^^^^^

[source, clojure]
----
(go
  [... do something asynchronously ...])
----

- always return a channel.
- put in a returned channel the restul of last expression.
- executes asynchronously.


The *chan* function
^^^^^^^^^^^^^^^^^^^

[source, clojure]
----
(chan)
----

- creates a new channel
- does not support nil values
- nil return value means channel is closed
- support different buffering strategies: fixed size, unbound (default), sliding, dropping.


The *<!* and *>!* functions
^^^^^^^^^^^^^^^^^^^^^^^^^^^

[source, clojure]
----
(go
  (<! (timeout 100))
  (.log js/console "finished"))
----

- `<!` represents a callback-less `take!`
- `>!` represents a callback-less `put!`
- in clojure them have blocking version of them: `<!!` and `>!!` and they does not
  requires of go macro, because they blocks the current thread.


Other resources
^^^^^^^^^^^^^^^

- http://clojure.com/blog/2013/06/28/clojure-core-async-channels.html
- http://yobriefca.se/blog/2014/06/01/combining-and-controlling-channels-with-core-dot-asyncs-merge-and-mix/
- http://yobriefca.se/blog/2014/06/04/publish-and-subscribe-with-core-dot-asyncs-pub-and-sub/
- http://www.purelyfunctional.tv/core-async
- http://www.lispcast.com/elm-frp-in-core-async


Ajax with core.async
--------------------

Introduction
~~~~~~~~~~~~

This step tries reproduce the examples from step4 (ajax related) but using core.async
for make the logic look like sync, removing visually accidental complexity of continuations/callbacks.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step5
----


Why
~~~

- Callbacks sucks.
- Unclear execution flow.
- We can do it better!
- with core.async, async code looks sync ;)


Add *core.async* dependency
~~~~~~~~~~~~~~~~~~~~~~~~~~~

Add the corresponding dependency of core.async on our project.clj:

._project.clj_
[source, clojure]
----
:dependencies [;; ...
               [org.clojure/core.async "0.1.346.0-17112a-alpha"]]
----


*core.cljs* changes
~~~~~~~~~~~~~~~~~~~

Convert our code from previos step to something like this:

.Partial content from _core.cljs_
[source, clojure]
----
(ns cljsworkshop.core
  (:require-macros [secretary.core :refer [defroute]]
                   [cljs.core.async.macros :refer [go]])
  (:require [goog.events :as events]
            [goog.dom :as dom]
            [secretary.core :as secretary]
            [cljs.core.async :refer [<! put! chan]])
  (:import goog.History
           goog.Uri
           goog.net.Jsonp))

(defn render-results [results]
  (let [results (js->clj results)]
    (reduce (fn [acc result]
              (str acc "<li>" result "</li>"))
            ""
            (second results))))

(defn listen [el type]
  (let [out (chan)]
    (events/listen el type (fn [e] (put! out e)))
    out))

(defn jsonp [uri]
  (let [out (chan)
        req (Jsonp. (Uri. uri))]
    (.send req nil (fn [res] (put! out res)))
    out))

(defroute home-path "/" []
  ;; Render initial html
  (set-html! app home-html)

  (let [clicks (listen (dom/getElement "searchbutton") "click")]
    (go (while true
          (<! clicks)
          (let [uri     (str search-url (.-value (dom/getElement "query")))
                results (<! (jsonp uri))]
            (set-html! (dom/getElement "results")
                       (render-results results)))))))
----

Now the code looks sync:

1. Waits a click.
2. Make a request to wikipedia.
3. Renders result.

A synchronous code makes it easier to reason about itself.


Hello World from Om component
-----------------------------

Introduction
~~~~~~~~~~~~

This is a first step that introduces *Om*.

Why:
^^^^

- Reactjs (functional approach for rendering dom)
- Global state management facilities built in.
- Customizable semantics. Fine grained control over how components store state.
- Out of the box snapshotable and undoable and these operations have no implementation
  complexity and little overhead.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step6
----

Add dependencies
~~~~~~~~~~~~~~~~

._project.clj_
[source, clojure]
----
:dependencies [;; ...
               [org.omcljs/om "0.8.8"]
               [prismatic/om-tools "0.3.10"]]
----


First om component
~~~~~~~~~~~~~~~~~~

Before see a more complex app, we'll try understand the basic of om components.

[source, clojure]
----
(ns mysamplens
  (:require [om.core :as om]
            [om-tools.dom :as dom]))

(defn mycomponent
  [app owner]
  (reify
    ;; Set the initial component state.
    om/IInitState
    (init-state [_]
      {:message "Hello world from local state"})

    ;; Render the component with current local state.
    om/IRenderState
    (render-state [_ {:keys [message]}]
      (dom/section
        (dom/div message)
        (dom/div (:message app))))))
----

*_reify_, what is this?*

_reify_ creates an anonymos object that implement one or more protocols.

_om_ components consists in any object that implements the `om/IRender` or
`om/IRenderState` protocols. Implementations for other protocols is optional.

In previous examples we have used a few number of protocols. Om comes with few other
but them comes out of this first example scope.

*Now, having defined a compoment, it a time to mount it.

[source, clojure]
----
(defonce state {:message "Hello world from global state."})

;; "app" is a id of dom element at index.html
(let [el (gdom/getElement "app")]
  (om/root mycomponent state {:target el}))
----


Time traveling with Om
----------------------


Introduction
~~~~~~~~~~~~

- The state of aplication is serializable, that makes easy and in deterministic way to reproduce
  a concrete state of the application.
- The union of ClojureScript and Reactjs makes some task, that is usually considered very complex,
  very easy and painless, such as the time traveling or the undo in a few lines of code.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step7
----


How it works
~~~~~~~~~~~~

Desgining the application with global state management facilities of om, we can easy make a
snapshot of the current state.

In Clojure(Script) an atom can be listened for changes:

[source, clojure]
----
;; Global applicatioon state
(def tasklist-state (atom {:entries []}))

;; Undo application state. An atom that will store
;; the snapshots of tasklist-state initialized with
;; initial @tasklist-state.
(def undo-state (atom {:entries [@tasklist-state]})

;; Watch a tasklist-state changes and snapshot them
;; into undo-state.
(add-watch tasklist-state :history
  (fn [_ _ _ n]
    (let [entries (:entries @undo-state)]
      (when-not (= (last entries) n)
        (swap! undo-state #(update-in % [:entries] conj n))))))
----

Now, each change in our application, is saved as snapshot in an other atom, and
with simple button we can revert the last change and restore the previous one.

.For it, we are created an other om component...
[source, clojure]
----
(defn do-undo
  [app]
  (when (> (count (:entries @app)) 1)
    ;; remove the last spapshot from the undo list.
    (om/transact! app :entries pop)

    ;; Restore the last snapshot into tasklist
    ;; application state
    (reset! tasklist-state (last (:entries @undo-state)))))

(defn undo
  [app owner]
  (reify
    om/IRender
    (render [_]
      (dom/section {:class "undo"
                    :style {:padding "5px"
                            :border "1px solid #ddd"}}
        (dom/section {:class "buttons"}
          (dom/input {:type "button"
                      :default-value "Undo"
                      :on-click (fn[_] (do-undo state))}))))))
----


Persisting Om state
-------------------

Introduction
~~~~~~~~~~~~

Now having experimented with timetraveling, let's try an little experimet making the state persistent.
For it we will use the previous example and html5 localstorage.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step8
----


Add _hodgepodge_ dependency
~~~~~~~~~~~~~~~~~~~~~~~~~~~

._project.clj_
[source, clojure]
----
:dependencies [;; ...
               [hodgepodge "0.1.3"]]
----


Add the persistence logic to our tasklist app
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.Add the corresponding _:require_ entry for _hodgepodge_
[source, clojure]
----
(ns cljsworkshop.core
  (:require [...]
            [hodgepodge.core :refer [local-storage]]))
----


.Add additional watcher to tasklist-state atom responsible of the persistence
[source, clojure]
----
;; Watch tasklist-state changes and
;; persists them in local storege.
(add-watch tasklist-state :persistece
  (fn [_ _ _ n]
    (println "Event:" n)
    (assoc! local-storage :taskliststate n)))
----

.Add code for restore stored state on app initialization.
[source, clojure]
----
;; Watch tasklist-state changes and
;; Get the persisted state, and if it exists
;; restore it on tasklist and undo states.
(when-let [state (:taskliststate local-storage)]
  (reset! tasklist-state state)
  (reset! undo-state {:entries [state]}))
----


Bonus: browser enabled repl
---------------------------

Introduction
~~~~~~~~~~~~

One of the reasons that Clojure rocks is that it has a REPL which gives developers
the most dynamic development experience possible. We would like to support this 
dynamic development experience in every environment where JavaScript runs.

There are many interesting environments in which JavaScript can run. And clojurescript
offers an abstraction for run a repl in different evaluation environments.

*In this section we will see, how to use a browser as evaluation environment.*


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step9
----


Add a little snippet of code to you cljs main file
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

[source, clojure]
----
(ns cljsworkshop.core
  (:require [...]
            [clojure.browser.repl :as repl]))

(repl/connect "http://localhost:9000/repl")
----


Crete a repl setup file
~~~~~~~~~~~~~~~~~~~~~~~

.*repl_browser.clj*
[source, clojure]
----
(require
  '[cljs.repl :as repl]
  '[cljs.repl.browser :as browser])

(repl/repl* (browser/repl-env)
  {:output-dir "out"
   :optimizations :none
   :cache-analysis false
   :source-map true})
----


Optionally create a helper script
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

That helps for start the repl easy.

.*start-browserrepl.sh*
[source, clojure]
----
#!/usr/bin/env bash
rlwrap lein trampoline run -m clojure.main repl_browser.clj
----

Add exection permissions:

[source, bash]
----
chmod +x start-browserrepl.sh
----


Start the repl
~~~~~~~~~~~~~~

Using the previosly created shell script:

[source, bash]
----
$ ./start-browserrepl.sh
Compiling client js ...
Waiting for browser to connect ...
To quit, type: :cljs/quit
----

And now navigate to your public page!

It will automatically connects to the repl and you will be able evaluate clojurescript 
code in the browser.

.Try evaluate the current app state
[source, clojure]
----
cljs.user=> (in-ns 'cljsworkshop.core)
cljsworkshop.core
cljsworkshop.core=> @tasklist-state
{:entries [{:completed false, :created-at "2014-12-08T11:32:10.677Z", :subject "task 1"}]}
nil
----


Bonus: standalone repl
----------------------

Continue in *step9*


Introduction
~~~~~~~~~~~~

Browser repl is awesome if you are building web app, but it is slightly tedious if
you are building a library, requiring a browser for something that not needs it.

For it, exists other evaluation environments like nodejs or java8 nashorn javascript
engine. You can start a nodejs repl (as example) much easier that browser repl.


Crete a repl setup file
~~~~~~~~~~~~~~~~~~~~~~~

.*repl_nodejs.clj*
[source, clojure]
----
(require
  '[cljs.repl :as repl]
  '[cljs.repl.node :as node])

(repl/repl* (node/repl-env)
  {:output-dir "out"
   :optimizations :none
   :cache-analysis false
   :source-map true})
----


Optionally create a helper script
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

That helps for start the repl easy.

.*start-noderepl.sh*
[source, clojure]
----
#!/usr/bin/env bash
rlwrap lein trampoline run -m clojure.main repl_nodejs.clj
----

Add exection permissions:

[source, bash]
----
chmod +x start-noderepl.sh
----


Start the repl and evaluate cljs
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Using the previosly created shell script:

[source, bash]
----
$ ./start-noderepl.sh
To quit, type: :cljs/quit
ClojureScript Node.js REPL server listening on 58603
ClojureScript:cljs.user> (require '[clojure.string :as str])

ClojureScript:cljs.user> (str/lower-case "Foo Bar")
"foo bar"
----


Bonus: production ready builds
------------------------------

Introduction
~~~~~~~~~~~~

At this moment, we have compiled clojurescript for development friendly environment. And
it is ok, but now it's time to deploy our application.

As we known, clojurescript uses google closure as compiler. Let see how we can configure
the production ready builds.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step10
----


Build configuration
~~~~~~~~~~~~~~~~~~~

Replace your old build configuration on your *project.clj* with this:


[source, clojure]
----
{:builds
 [{:id "devel"
   :source-paths ["src/cljs"]
   :compiler {:output-to "resources/public/js/app.js"
              :output-dir "resources/public/js/out-devel"
              :source-map true
              :optimizations :none
              :cache-analysis false
              :asset-path "/static/js/out-devel"
              :main cljsworkshop.core
              :pretty-print true}}
  {:id "prod"
   :source-paths ["src/cljs"]
   :compiler {:output-to "resources/public/js/app.js"
              :output-dir "resources/public/js/out-prod"
              :source-map "resources/public/js/app.js.map"
              :closure-warnings {:externs-validation :off}
              :optimizations :advanced
              :cache-analysis false
              :asset-path "/static/js/out-prod"
              :main cljsworkshop.core
              :pretty-print false}}]})
----

The *devel* is a existing one, and *prod* is a new created. Observe the differences:

- `:optimizations` is not set to `:advanced`
- `:pretty-print` is set to `false`

Very easy, not run the compiler in *prod* mode:

[source, text]
----
[3/5.0.7]niwi@niwi:~/cljs-workshop> lein cljsbuild auto prod
Compiling ClojureScript.
Compiling "resources/public/js/app.js" from ["src/cljs"]...
Successfully compiled "resources/public/js/app.js" in 17.396 seconds.
----

And observe that your web is running like with previos compilation mode but
loads only one little js.


Bonus: using third party libraries
----------------------------------

Introduction
~~~~~~~~~~~~

Is well known that not all third party libraries like jQuery, momentjs or react works
out of the box with closure compiler.

But is not a problem. Clojurescript thanks to some closure compiler options allow easy
path for use external libraries.

For this purpose it exposes `:foreign-libs` and `:externs` compiler options.

The first one, allows setup the dependencies and the module for third party library. And
the second allows setup a file that helps closure compiler to not mangling symbol names
that corresponds to third party library usage.


Let start with
~~~~~~~~~~~~~~

[source, text]
----
git reset --hard
git checkout step11
----


Integrating momentjs
~~~~~~~~~~~~~~~~~~~~

Add this options to your build configurations:

[source, clojure]
----
:foreign-libs [{:file "js/moment.js"
                :file-min "js/moment.min.js"
                :provides ["cljsworkshop.moment"]}]
:externs ["js/moment.min.js"]
:closure-warnings {:externs-validation :off}
----

Externs file is a file with special only definition javascript. They are very tedios to write
and in this case we can use the minified source as externs file.

Using the minified source as externs file raises a lot of warnings, that in majority of cases
we can ignore.


Use momentjs in our app
~~~~~~~~~~~~~~~~~~~~~~~

For use the momentjs in our application, you should add it in
our require part of the ns macro:

[source, clojure]
----
(ns cljsworkshop.core
  (:require [...]
            [cljsworkshop.moment :as moment]))
----

Independently of the alias used in requirement statement, the moment variable is set globally
and it should be used through the js special prefix. Like this

[source, clojure]
----
(def now (js/moment))
----


Other Om resources
------------------

- http://swannodette.github.io/2013/12/17/the-future-of-javascript-mvcs/
- http://swannodette.github.io/2013/12/31/time-travel/
- http://blog.circleci.com/local-state-global-concerns/
- http://blog.getprismatic.com/om-sweet-om-high-functional-frontend-engineering-with-clojurescript-and-react/
- http://www.slideshare.net/borgesleonardo/high-performance-web-apps-in-om-react-and-clojurescript
- http://murilopereira.com/the-case-for-reactjs-and-clojurescript/
